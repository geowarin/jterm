plugins {
  id "com.jfrog.bintray" version "1.3.1"
}

group 'com.github.geowarin'
version '0.3'

apply plugin: 'maven'
apply plugin: 'maven-publish'
apply plugin: 'groovy'

sourceCompatibility = 1.7

repositories {
  mavenCentral()
}

dependencies {
  compile('org.codehaus.groovy:groovy-all:2.4.5') { ext.optional = true }
  compile 'jline:jline:2.13'
  testCompile "org.spockframework:spock-core:1.0-groovy-2.4"
}

task sourcesJar(type: Jar, dependsOn: classes) {
  classifier = 'sources'
  from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
  classifier = 'javadoc'
  from javadoc.destinationDir
}

artifacts {
  archives sourcesJar, javadocJar
}


publishing {
  publications {
    JarPublication(MavenPublication) {
      from components.java
      artifact sourcesJar
      artifact javadocJar
      pom.withXml {
        def depMap = configurations.compile.dependencies.collectEntries { [it.name, it] }
        asNode().dependencies.dependency.findAll {
          def dep = depMap.get(it.artifactId.text())
          return dep?.hasProperty('optional') && dep.optional
        }.each {
          if (it.optional) {
            it.optional.value = 'true'
          } else {
            it.appendNode('optional', 'true')
          }
        }
      }

    }
  }
}


bintray {
  user = bintrayUser
  key = bintrayKey
  publications = ['JarPublication']
  pkg {
    repo = 'maven'
    name = 'JTerm'
    desc = 'A friendly java/groovy library to collect user inputs in commandline applications'
    licenses = ['MIT']
    vcsUrl = 'https://github.com/geowarin/jterm.git'
    version {
      name = project.version
      desc = 'A friendly java/groovy library to collect user inputs in commandline applications'
    }
  }
}
